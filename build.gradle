/*
 * MIT License
 *
 * Copyright (c) 2017 Barracks Inc.
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

buildscript {
    ext {
        springBootVersion = '1.5.1.RELEASE'
        springVersion = '4.3.6.RELEASE'
        dropwizardMetricsVersion = '3.1.2'
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
    repositories {
        maven {
            url "http://repository.jboss.org/nexus/content/groups/public/"
        }
        mavenLocal()
        mavenCentral()
        jcenter()
    }
}

group = projectGroup

def buildNumber = System.getenv("BUILD_NUMBER")

apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'pmd'
apply plugin: 'findbugs'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'


sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile group: 'io.dropwizard.metrics', name: 'metrics-core', version: dropwizardMetricsVersion
    compile group: 'io.dropwizard.metrics', name: 'metrics-graphite', version: dropwizardMetricsVersion
    compile group: 'io.dropwizard.metrics', name: 'metrics-jvm', version: dropwizardMetricsVersion
    compile group: 'io.dropwizard.metrics', name: 'metrics-servlet', version: dropwizardMetricsVersion
    compile group: 'com.ryantenney.metrics', name: 'metrics-spring', version: '3.1.3'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.6.5'
    compile group: 'cz.jirutka.spring', name: 'spring-rest-exception-handler', version: '1.2.0'

    compileOnly group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: springBootVersion
    compileOnly group: 'org.springframework.boot', name: 'spring-boot-starter-aop', version: springBootVersion
    compileOnly group: 'org.springframework.boot', name: 'spring-boot-starter-actuator', version: springBootVersion
    compileOnly group: 'org.springframework.boot', name: 'spring-boot-starter-data-mongodb', version: springBootVersion
    compileOnly group: 'org.springframework.boot', name: 'spring-boot-starter-data-rest', version: springBootVersion
    compileOnly group: 'javax.servlet', name: 'javax.servlet-api', version: '3.1.0'

    testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: springBootVersion
    testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-aop', version: springBootVersion
    testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-actuator', version: springBootVersion
    testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-data-mongodb', version: springBootVersion
    testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-data-rest', version: springBootVersion
    testCompile group: 'javax.servlet', name: 'javax.servlet-api', version: '3.1.0'

    testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: springBootVersion
    testCompile group: 'org.assertj', name: 'assertj-core', version: '3.6.2'
}

jar {
    version = (System.getenv("BRANCH_NAME") ?: "LOCAL") + '-' + (buildNumber ?: "SNAPSHOT")
}

jacoco {
    toolVersion = "0.7.6.201602180812"
}

test {
    jacoco {
        destinationFile = file("$buildDir/jacoco/jacoco.exec")
    }
}

jacocoTestReport {
    executionData = fileTree(dir: "$buildDir/jacoco", include: '**/*.exec')
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

check.dependsOn jacocoTestReport

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            groupId projectGroup
            artifactId rootProject.name
            version jar.version
        }
    }
    repositories {
        maven {
            name "bintray"
            url "https://api.bintray.com/content/barracksiot/"
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    pkg {
        repo = 'io.barracks.libs'
        name = 'common-libs'
        userOrg = 'barracksiot'
        publications = ['mavenJava']
        version {
            name = jar.version
        }
    }
}


task wrapper(type: Wrapper) {
    gradleVersion = '2.12'
}

tasks.withType(FindBugs) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}